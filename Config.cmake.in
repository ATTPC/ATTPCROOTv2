@PACKAGE_INIT@

set(CMAKE_PREFIX_PATH ${CMAKE_PREFIX_PATH} @CMAKE_PREFIX_PATH@)
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "@CMAKE_SOURCE_DIR@/cmake/modules" )
include(CMakeFindDependencyMacro)

message(STATUS "Searching for dependencies for @PROJECT_NAME@ in: ${CMAKE_PREFIX_PATH}")
message(STATUS "Searching for FindModules in: ${CMAKE_MODULE_PATH}")

# Required dependencies
find_dependency(FairRoot)
find_dependency(ROOT)
if(NOT TARGET ROOT::VMC)
  find_dependency(VMC)
  set_target_properties(VMCLibrary PROPERTIES IMPORTED_GLOBAL TRUE)
  add_library(ROOT::VMC ALIAS VMCLibrary)
endif()
find_dependency(FairLogger)
find_dependency(fmt)
find_dependency(Boost)
if(NOT DEFINED CMAKE_SUPPRESS_DEVELOPER_WARNINGS)
  set(CMAKE_SUPPRESS_DEVELOPER_WARNINGS 1)
  set(_temp_mod_dev_warning 1)
endif()
find_dependency(PCL QUIET)
if(_temp_mod_dev_warning)
  unset(CMAKE_SUPPRESS_DEVELOPER_WARNINGS)
endif()

# Optional dependencies (TODO: Need to populate at configure time...)
if(@HiRAEVT_FOUND@)
  find_dependency(HiRAEVT)
endif()
if(@GENFIT2_FOUND@)
  find_dependency(GENFIT2)
endif()

# include the exported targets
include ("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
